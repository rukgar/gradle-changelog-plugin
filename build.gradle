plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version "$pluginPublishVersion"
    id 'maven-publish'
    // Comment out the following line prior to first build, then run 'publishToMavenLocal' task,
    // then uncomment this line. Also see the 'changelog' task, below.
    id 'com.github.rukgar.gradle-conventional-changelog' version "$changelogPluginVersion"
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "6.6.1"
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url  "https://dl.bintray.com/ggranum/gradle-conventional-changelog"
    }
}


project.ext {
    pluginId = rootProject['project.pluginId']
    githubHandle = "rukgar"
}

version = rootProject['project.version']
group = "com.github.${project.githubHandle}"

pluginBundle {
    website = "https://github.com/${project.githubHandle}/gradle-changelog-plugin/"
    vcsUrl = "https://github.com/${project.githubHandle}/gradle-changelog-plugin/"
    tags = ['conventional changelog', 'git']
}

gradlePlugin {
    plugins {
        conventionalChangelogPlugin {
            id = project.pluginId
            displayName = 'Conventional Changelog Generation plugin for Gradle'
            description = "Use the format established by Google's Angular team to automatically generate a changelog file from git commit messages."
            implementationClass = 'org.gradle.api.plugins.changelog.GitChangelogPlugin'
        }
    }
}


dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Conventional Changelog For Gradle',
                'Implementation-Version': project.version,
                'Built-By': 'David Campos',
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}


task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        mavenLocal()
    }
}


// Comment out the following task prior to first build, then run 'publishToMavenLocal' task, then uncomment.
changelog {
    file "CHANGELOG.md"
    versionNum "v$project.version"
    versionText "Lucky Bird"
    repoUrl "https://github.com/rukgar/gradle-changelog-plugin"
    match "^fix|^feat|^doc|^perf|^refactor|BREAKING"
    //from "0.3.0"
    //to "HEAD"
}
