plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'maven-publish'
    // Comment out the following line prior to first build, then run 'publishToMavenLocal' task,
    // then uncomment this line. Also see the 'changelog' task, below.
    id 'com.geoffgranum.gradle-conventional-changelog' version '0.3.1'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "6.6.1"
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url  "https://dl.bintray.com/ggranum/gradle-conventional-changelog"
    }
}


project.ext.pluginId = rootProject['project.pluginId']
version = rootProject['project.version']
group = 'com.geoffgranum'

pluginBundle {
    website = 'https://github.com/ggranum/gradle-changelog-plugin/'
    vcsUrl = 'https://github.com/ggranum/gradle-changelog-plugin/'
    tags = ['conventional changelog', 'git']
}

gradlePlugin {
    plugins {
        conventionalChangelogPlugin {
            id = project.pluginId
            displayName = 'Conventional Changelog Generation plugin for Gradle'
            description = "Use the format established by Google's Angular team to automatically generate a changelog file from git commit messages."
            implementationClass = 'org.gradle.api.plugins.changelog.GitChangelogPlugin'
        }
    }
}


dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Conventional Changelog For Gradle',
                'Implementation-Version': project.version,
                'Built-By': 'Geoff Granum',
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}


task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        mavenLocal()
    }
}


// Comment out the following task prior to first build, then run 'publishToMavenLocal' task, then uncomment.
changelog {
    file "CHANGELOG.md"
    versionNum "v$project.version"
    versionText "Fatalist Zebra"
    repoUrl "https://github.com/ggranum/gradle-changelog-plugin"
    match "^fix|^feat|^fix|^perf|^refactor|BREAKING"
    from "0.3.0"
    //to "HEAD"
}
