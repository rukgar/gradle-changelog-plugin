buildscript {
    repositories {
        [
                'http://dl.bintray.com/andresaraujo/gradle-plugins',
                'http://jcenter.bintray.com',
                '../repo'
        ].each { repo ->
            maven { url repo }
        }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
        classpath group: 'io.github.marcpoppleton.changelog', name: 'git-changelog', version: '0.1.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'bintray'
apply plugin: 'changelog'

group = 'io.github.marcpoppleton.changelog'
version = '0.1.3'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile "org.spockframework:spock-core:0.7-groovy-1.8"
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Changelog plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle Changelog plugin')
                root.appendNode('description', 'Gradle plugin for generating changelog from commit logs.\n Forked from Andres Araujos plugin')
                root.appendNode('inceptionYear', '2015')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def marcpoppleton = developers.appendNode('developer')
                marcpoppleton.appendNode('id', 'marcpoppleton')
                marcpoppleton.appendNode('name', 'Marc Poppleton')
                marcpoppleton.appendNode('email', 'marc.poppleton@gmail.com')
            }
        }
        repositories {
            maven {
                url "../repo"
            }

        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : ""
    key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : ""
    publications = ['mavenJava']

    pkg {
        repo = 'gradle-plugins'
        name = 'gradle-changelog-plugin'
        licenses = ['Apache-2.0']
    }

    //dryRun = true
}

changelog {
    file "CHANGELOG.md"
    versionNum "v$project.version"
    subtitle "Electric Lama"
    repoUrl "https://github.com/marcpoppleton/gradle-changelog-plugin"
    match "^fix|^feat|^fix|^perf|^refactor|BREAKING"

    //from "0.1.0"
    //to "HEAD"
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}